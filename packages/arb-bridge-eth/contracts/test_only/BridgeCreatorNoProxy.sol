// SPDX-License-Identifier: Apache-2.0

/*
 * Copyright 2021, Offchain Labs, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pragma solidity ^0.6.11;

import "../bridge/Bridge.sol";
import "../bridge/SequencerInbox.sol";
import "../bridge/Inbox.sol";
import "../bridge/Outbox.sol";
import "../rollup/RollupEventBridge.sol";

import "../bridge/interfaces/IBridge.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract BridgeCreatorNoProxy is Ownable {
    Bridge delayedBridgeTemplate;
    SequencerInbox sequencerInboxTemplate;
    Inbox inboxTemplate;
    RollupEventBridge rollupEventBridgeTemplate;
    Outbox outboxTemplate;

    event TemplatesUpdated();

    constructor() public Ownable() {
        delayedBridgeTemplate = new Bridge();
        sequencerInboxTemplate = new SequencerInbox();
        inboxTemplate = new Inbox();
        rollupEventBridgeTemplate = new RollupEventBridge();
        outboxTemplate = new Outbox();
    }

    function updateTemplates(
        address _delayedBridgeTemplate,
        address _sequencerInboxTemplate,
        address _inboxTemplate,
        address _rollupEventBridgeTemplate,
        address _outboxTemplate
    ) external onlyOwner {
        delayedBridgeTemplate = Bridge(_delayedBridgeTemplate);
        sequencerInboxTemplate = SequencerInbox(_sequencerInboxTemplate);
        inboxTemplate = Inbox(_inboxTemplate);
        rollupEventBridgeTemplate = RollupEventBridge(_rollupEventBridgeTemplate);
        outboxTemplate = Outbox(_outboxTemplate);

        emit TemplatesUpdated();
    }

    struct CreateBridgeFrame {
        ProxyAdmin admin;
        Bridge delayedBridge;
        SequencerInbox sequencerInbox;
        Inbox inbox;
        RollupEventBridge rollupEventBridge;
        Outbox outbox;
    }

    function createBridge(
        address adminProxy,
        address rollup,
        address sequencer,
        uint256 sequencerDelayBlocks,
        uint256 sequencerDelaySeconds
    )
        external
        returns (
            Bridge,
            SequencerInbox,
            Inbox,
            RollupEventBridge,
            Outbox
        )
    {
        CreateBridgeFrame memory frame;
        {
            frame.delayedBridge = delayedBridgeTemplate;
            frame.sequencerInbox = sequencerInboxTemplate;
            frame.inbox = inboxTemplate;
            frame.rollupEventBridge = rollupEventBridgeTemplate;
            frame.outbox = outboxTemplate;
        }

        frame.delayedBridge.initialize();
        frame.sequencerInbox.initialize(
            IBridge(frame.delayedBridge),
            sequencer,
            sequencerDelayBlocks,
            sequencerDelaySeconds
        );
        frame.inbox.initialize(IBridge(frame.delayedBridge));
        frame.rollupEventBridge.initialize(address(frame.delayedBridge), rollup);
        frame.outbox.initialize(rollup, IBridge(frame.delayedBridge));

        frame.delayedBridge.setInbox(address(frame.inbox), true);
        frame.delayedBridge.transferOwnership(rollup);

        return (
            frame.delayedBridge,
            frame.sequencerInbox,
            frame.inbox,
            frame.rollupEventBridge,
            frame.outbox
        );
    }
}
