// SPDX-License-Identifier: Apache-2.0

/*
 * Copyright 2020, Offchain Labs, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pragma solidity ^0.6.11;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/token/ERC20/SafeERC20.sol";
import "@openzeppelin/contracts/utils/Create2.sol";
import "@openzeppelin/contracts/utils/Address.sol";

import { L1ArbitrumMessenger } from "../../libraries/gateway/ArbitrumMessenger.sol";
import "../../libraries/gateway/ArbitrumGateway.sol";
import "../../libraries/IERC677.sol";

/**
 * @title Common interface for gatways on L1 messaging to Arbitrum.
 */
abstract contract L1ArbitrumGateway is L1ArbitrumMessenger, ArbitrumGateway {
    using SafeERC20 for IERC20;
    using Address for address;

    address public inbox;

    modifier onlyCounterpartGateway() virtual override {
        address l2ToL1Sender = getL2ToL1Sender(inbox);
        require(isCounterpartGateway(l2ToL1Sender), "ONLY_COUNTERPART_GATEWAY");
        _;
    }

    function _initialize(
        address _l2Counterpart,
        address _router,
        address _inbox
    ) internal virtual {
        ArbitrumGateway._initialize(_l2Counterpart, _router);
        // L1 gateway must have a router
        require(_router != address(0), "BAD_ROUTER");
        require(_inbox != address(0), "BAD_INBOX");
        inbox = _inbox;
    }

    /**
     * @notice Finalizes a withdrawal via Outbox message; callable only by L2Gateway.outboundTransfer
     * @param _token L1 address of token being withdrawn from
     * @param _from initiator of withdrawal
     * @param _to address the L2 withdrawal call set as the destination.
     * @param _amount Token amount being withdrawn
     * @param _data encoded exitNum (Sequentially increasing exit counter determined by the L2Gateway) and additinal hook data
     */
    function finalizeInboundTransfer(
        address _token,
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _data
    ) external payable virtual override onlyCounterpartGateway returns (bytes memory) {
        (uint256 exitNum, bytes memory callHookData) = parseInboundData(_data);

        (_to, callHookData) = getExternalCall(exitNum, _to, callHookData);

        if (callHookData.length > 0) {
            bool success;
            try this.inboundEscrowAndCall(_token, _amount, _from, _to, callHookData) {
                success = true;
            } catch {
                // if reverted, then credit _from's account
                inboundEscrowTransfer(_token, _from, _amount);
                // success default value is false
            }
            emit TransferAndCallTriggered(success, _from, _to, _amount, callHookData);
        } else {
            inboundEscrowTransfer(_token, _to, _amount);
        }

        emit InboundTransferFinalized(_token, _from, _to, exitNum, _amount, _data);
        return bytes("");
    }

    function gasReserveIfCallRevert() public pure virtual override returns (uint256) {
        // amount of gas necessary to send user tokens in case
        // of the "onTokenTransfer" call consumes all available gas
        return 30000;
    }

    function getExternalCall(
        uint256 _exitNum,
        address _initialDestination,
        bytes memory _initialData
    ) public view virtual returns (address target, bytes memory data) {
        // current destination can be changed for tradeable exits in a super class
        target = _initialDestination;
        data = _initialData;
    }

    function parseInboundData(bytes calldata _data)
        public
        pure
        virtual
        returns (uint256 _exitNum, bytes memory _extraData)
    {
        // this data is encoded by the counterpart gateway, so this shouldn't revert
        (_exitNum, _extraData) = abi.decode(_data, (uint256, bytes));
    }

    function inboundEscrowTransfer(
        address _l1Token,
        address _dest,
        uint256 _amount
    ) internal virtual override {
        IERC20(_l1Token).safeTransfer(_dest, _amount);
    }

    function createOutboundTx(
        address _l1Token,
        address _from,
        address _to,
        uint256 _amount,
        uint256 _maxGas,
        uint256 _gasPriceBid,
        uint256 _maxSubmissionCost,
        bytes memory _extraData
    ) internal virtual returns (uint256) {
        // msg.value is sent, but 0 is set to the L2 call value
        // the eth sent is used to pay for the tx's gas
        return
            sendTxToL2(
                _from,
                0, // l2 call value 0 by default
                _maxSubmissionCost,
                _maxGas,
                _gasPriceBid,
                getOutboundCalldata(_l1Token, _from, _to, _amount, _extraData)
            );
    }

    function sendTxToL2(
        address _user,
        uint256 _l2CallValue,
        uint256 _maxSubmissionCost,
        uint256 _maxGas,
        uint256 _gasPriceBid,
        bytes memory _data
    ) internal virtual returns (uint256) {
        return
            sendTxToL2(
                inbox,
                counterpartGateway,
                _user,
                _l2CallValue,
                _maxSubmissionCost,
                _maxGas,
                _gasPriceBid,
                _data
            );
    }

    /**
     * @notice Deposit ERC20 token from Ethereum into Arbitrum. If L2 side hasn't been deployed yet, includes name/symbol/decimals data for initial L2 deploy. Initiate by GatewayRouter.
     * @param _l1Token L1 address of ERC20
     * @param _to account to be credited with the tokens in the L2 (can be the user's L2 account or a contract)
     * @param _amount Token Amount
     * @param _maxGas Max gas deducted from user's L2 balance to cover L2 execution
     * @param _gasPriceBid Gas price for L2 execution
     * @param _data encoded data from router and user
     * @return res abi encoded inbox sequence number
     */
    //  * @param maxSubmissionCost Max gas deducted from user's L2 balance to cover base submission fee
    function outboundTransfer(
        address _l1Token,
        address _to,
        uint256 _amount,
        uint256 _maxGas,
        uint256 _gasPriceBid,
        bytes calldata _data
    ) public payable virtual override returns (bytes memory res) {
        address _from;
        uint256 seqNum;
        {
            uint256 _maxSubmissionCost;
            bytes memory extraData;
            (_from, _maxSubmissionCost, extraData) = parseOutboundData(_data);

            require(_l1Token.isContract(), "L1_NOT_CONTRACT");
            // this validates if the l1 token was set correctly
            address l2Token = _calculateL2TokenAddress(_l1Token);

            outboundEscrowTransfer(_l1Token, _from, _amount);

            seqNum = createOutboundTx(
                _l1Token,
                _from,
                _to,
                _amount,
                _maxGas,
                _gasPriceBid,
                _maxSubmissionCost,
                extraData
            );
        }

        emit OutboundTransferInitiated(_l1Token, _from, _to, seqNum, _amount, _data);
        return abi.encode(seqNum);
    }

    function outboundEscrowTransfer(
        address _l1Token,
        address _from,
        uint256 _amount
    ) internal virtual {
        // escrow funds in gateway
        IERC20(_l1Token).safeTransferFrom(_from, address(this), _amount);
    }

    function parseOutboundData(bytes memory _data)
        internal
        view
        virtual
        returns (
            address _from,
            uint256 _maxSubmissionCost,
            bytes memory _extraData
        )
    {
        if (isRouter(msg.sender)) {
            // router encoded
            (_from, _extraData) = abi.decode(_data, (address, bytes));
        } else {
            _from = msg.sender;
            _extraData = _data;
        }
        // user encoded
        (_maxSubmissionCost, _extraData) = abi.decode(_extraData, (uint256, bytes));
    }

    function getOutboundCalldata(
        address _l1Token,
        address _from,
        address _to,
        uint256 _amount,
        bytes memory _data
    ) public view virtual override returns (bytes memory outboundCalldata) {
        bytes memory emptyBytes = "";

        outboundCalldata = abi.encodeWithSelector(
            ArbitrumGateway.finalizeInboundTransfer.selector,
            _l1Token,
            _from,
            _to,
            _amount,
            abi.encode(emptyBytes, _data)
        );

        return outboundCalldata;
    }
}
